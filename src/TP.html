<html>

<head>
    <link rel="stylesheet" href="estilo.css">
    <title>Trabajo Pr&#225;ctico</title>
    <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

    <script type="text/javascript" src="js/gl-matrix.js"></script>
    <script type="text/javascript" src="js/webgl-utils.js"></script>
    <script type="text/javascript" src="js/dat.gui.min.js"></script>
    <script type="text/javascript" src="js/jquery-2.2.4.min.js"></script>
    <script type="text/javascript" src="js/Util/Calculator.js"></script>
    <script type="text/javascript" src="js/GraphicalUserInterfaceControl/GraphicalUserInterfaceControl.js"></script>
    <script type="text/javascript" src="js/TextureHandler.js"></script>
    <script type="text/javascript" src="js/Definitions/Definitions.js"></script>
    <script type="text/javascript" src="js/Camera/MouseController.js"></script>
    <script type="text/javascript" src="js/Camera/KeyboardController.js"></script>
    <script type="text/javascript" src="js/Camera/MoveBackward.js"></script>
    <script type="text/javascript" src="js/Camera/MoveForward.js"></script>
    <script type="text/javascript" src="js/Camera/MoveRight.js"></script>
    <script type="text/javascript" src="js/Camera/MoveLeft.js"></script>
    <script type="text/javascript" src="js/Camera/ToogleCamera.js"></script>
    <script type="text/javascript" src="js/Shader/Compiler.js"></script>
    <script type="text/javascript" src="js/Shader/Linker.js"></script>
    <script type="text/javascript" src="js/ModelViewMatrixStack.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/WebGlRenderingContext.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/ThreeDimensionRenderingContext.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/TwoDimensionRenderingContext.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/GraphicContainer.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/SceneGraphicContainer.js"></script>
    <script type="text/javascript" src="js/GraphicContainer/RiverMapGraphicContainer.js"></script>
    <script type="text/javascript" src="js/Curves/Bspline.js"></script>
    <script type="text/javascript" src="js/Curves/BsplineMatrix.js"></script>
    <script type="text/javascript" src="js/AnimationFrame/AnimationFrame.js"></script>
    <script type="text/javascript" src="js/AnimationFrame/Scene.js"></script>

    <script type="text/javascript" src="js/GraphicalObject/GraphicalObject.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Cube.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Cylinder.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/SweptSurface.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/RevolutionSurface.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Grass.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/GrassLeftSide.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/GrassRightSide.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Street.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/TowerBody.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/TowerJunction.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Tower.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/BridgeTensor.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Bridge.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Grid.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Water.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Basin.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Ground.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Sky.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/Tree.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/TreeCrown.js"></script>
    <script type="text/javascript" src="js/GraphicalObject/TreeTrunk.js"></script>
    <script type="text/javascript" src="js/Camera/Camera.js"></script>
    <script type="text/javascript" src="js/Camera/Orbital.js"></script>
    <script type="text/javascript" src="js/Camera/PedestrianCamera.js"></script>
    <script type="text/javascript" src="js/AnimationFrame/RiverMap.js"></script>
    <script id="shader-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;
			attribute vec3 aVertexTangent;
			attribute vec3 aVertexBinormal;
			attribute vec2 aTextureCoord;

			uniform mat4 uInverseVMatrix;
			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat3 uNMatrix;

            uniform bool uUseReflection;
            uniform bool uDrawSkyBox;

            uniform bool uUseWaveEffect;
			uniform float uWavesAmplitudes[4];
			uniform vec2 uWavesDirections[4];
			uniform float uWavesFrequencies[4];
			uniform float uWavesPhases[4];
			uniform vec2 uTextureWavesDisplacements[4];
			uniform float uWaveTime;

			varying vec3 normalInterp;
			varying vec3 tangentInterp;
			varying vec3 binormalInterp;

			varying vec3 vertPos;
			varying vec3 position;
			varying vec2 vTextureCoord;
			varying vec2 vWaveTextureCoords[4];

            varying vec3 reflectDir;
            float dWaveDz(float z, float x, int i) {
				float phaseTerm = uWaveTime * uWavesPhases[i];
				float a = uWavesFrequencies[i] * uWavesDirections[i].x * uWavesAmplitudes[i];
				return a * cos(dot(uWavesDirections[i], vec2(z, x)) * uWavesFrequencies[i] + phaseTerm);
			}

			float dWaveDx(float z, float x, int i) {
				float phaseTerm = uWaveTime * uWavesPhases[i];
				float a = uWavesFrequencies[i] * uWavesDirections[i].y * uWavesAmplitudes[i];
				return a * cos(dot(uWavesDirections[i], vec2(z, x)) * uWavesFrequencies[i] + phaseTerm);
			}

			vec3 waveNormal(float z, float x) {
				float dx = 0.0;
				float dy = 1.0;
				float dz = 0.0;
				for (int i = 0; i != 4; i++) {
					dx += dWaveDx(z, x, i);
					dz += dWaveDz(z, x, i);
				}
				return normalize(vec3(-dx, 1.0, -dz));
			}

			float wave(float z, float x, int i) {
				float phaseTerm = uWaveTime * uWavesPhases[i];
				return uWavesAmplitudes[i] * sin(dot(uWavesDirections[i], vec2(z, x)) * uWavesFrequencies[i] + phaseTerm);
			}

			float waveHeight(float z, float x) {
				float height = 0.0;
				for (int i = 0; i != 4; i++) {
					height += wave(z, x, i);
				}
				return height;
			}

			vec2 waveTextureCoord(int i) {
				return aTextureCoord + uTextureWavesDisplacements[i] * uWaveTime;
			}
			void main(){
				float x = aVertexPosition.x;
				float y = aVertexPosition.y;
				float z = aVertexPosition.z;
                position = aVertexPosition;
				vec3 normal = aVertexNormal;
				vec3 tangent = aVertexTangent;
				vec3 binormal = aVertexBinormal;

				vTextureCoord = aTextureCoord;

                if (uUseWaveEffect) {
					y = waveHeight(z, x);

					normal = waveNormal(z, x);

					for (int i = 0; i != 4; i++) {
						vWaveTextureCoords[i] = waveTextureCoord(i);
					}
				}

    			gl_Position = uPMatrix * uMVMatrix * vec4(x, y, z, 1.0);
    			vec4 vertPos4 = uMVMatrix * vec4(x, y, z, 1.0);
    			vertPos = vec3(vertPos4) / vertPos4.w;

    			normalInterp = normalize(uNMatrix * normal);
    			tangentInterp = normalize(uNMatrix * tangent);
    			binormalInterp = normalize(uNMatrix * binormal);

                if (uDrawSkyBox) {
                    reflectDir = vec3(x, y, z);
                } else {
                    if (uUseReflection) {
                        vec3 view = normalize(vertPos);
                        reflectDir = reflect(view, normalInterp);
                        reflectDir = vec3(uInverseVMatrix * vec4(reflectDir, 0.0));
                    }
                }
			}

    </script>

    <script id="shader-fs" type="x-shader/x-fragment">
			uniform bool uUseNormalMap;
			uniform bool uUseDiffuseMap;
			uniform bool isTower;
			precision mediump float;
			uniform sampler2D uSampler;
			uniform sampler2D uNormalSampler;
			uniform samplerCube uCubeSampler;

            uniform bool uUseWaveEffect;

            uniform bool uUseBlending;
			uniform float uAlpha;

            uniform bool uUseReflection;
            uniform bool uDrawSkyBox;
            uniform float uReflectFactor;

			varying vec3 normalInterp;
			varying vec3 tangentInterp;
			varying vec3 binormalInterp;

			varying vec3 vertPos;
			varying vec2 vTextureCoord;
			varying vec3 reflectDir;

			struct Light{
    			vec3 Position; // Light position in eye coords.
    			vec3 La; // Ambient light intensity
    			vec3 Ld; // Diffuse light intensity
    			vec3 Ls; // Specular light intensity
			};
			uniform Light uLight;

			struct Material{
				vec3 Ka; // Ambient light
    			vec3 Kd; // Diffuse light
    			vec3 Ks; // Specular light
    			float Shininess;
			};

			uniform Material uMaterial;
			vec4 getFragColor (vec3 normal, vec4 cubemapColor, vec4 textureColor, vec3 vLight) {
				if (uUseReflection) {
						return vec4(mix(textureColor, cubemapColor, uReflectFactor).rgb * vLight, textureColor.a).rgba;
    			}
                if (uDrawSkyBox) {
                    return cubemapColor;
                }
                if (uUseDiffuseMap) {
                    vec3 lightDir = normalize(uLight.Position - vertPos);
                    vec3 diffuse = textureColor.rgb * uLight.Ld * max(dot(lightDir,normal), 0.0) * uMaterial.Kd;
                    return vec4(diffuse, 1.0);
                }
                 return vec4(textureColor.rgb * vLight, textureColor.a);
			}
            vec3 phongModel (vec3 vertPos, vec3 normal) {
                vec3 lightDir = normalize(uLight.Position - vertPos);
                vec3 lightReflectDir = reflect(-lightDir, normal);
                vec3 viewDir = normalize(-vertPos);
                vec3 ambient = uLight.La * uMaterial.Ka;
                float lambertian = max(dot(lightDir,normal), 0.0);
                vec3 diffuse = lambertian * uLight.Ld * uMaterial.Kd;
                vec3 specular = vec3(0.0);
                if(lambertian > 0.0) {
                    float specAngle = max(dot(lightReflectDir, viewDir), 0.0);
                    specular = uLight.Ls * uMaterial.Ks * pow(specAngle, uMaterial.Shininess);
                }
                return ambient + diffuse + specular;
           }
			void main() {
    			vec3 normal = normalize(normalInterp);

                if (uUseNormalMap){
    				vec3 tangent = normalize(tangentInterp);
    				vec3 binormal = normalize(binormalInterp);

    				mat3 tbn = mat3(tangent, binormal, normal);
    				vec3 normalCoordinate;
    				if (isTower) {
                        normalCoordinate = texture2D(uNormalSampler, vec2(vTextureCoord.s, vTextureCoord.t)).xyz * 2.0 - 1.0 ;
    				} else {
                        normalCoordinate = texture2D(uNormalSampler, vec2(vTextureCoord.s, vTextureCoord.t)).xyz;
    				}
    				//Transform the normal vector in the RGB channels to tangent space
    				normal = normalize(tbn * normalCoordinate);
    			}
    			vec3 vLight = phongModel(vertPos, normal);

    			vec4 textureColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
    			vec4 cubemapColor = textureCube(uCubeSampler, reflectDir);
    			if (uUseBlending) {
    				textureColor.a *= uAlpha;
    			}
    			gl_FragColor = getFragColor(normal, cubemapColor, textureColor, vLight);
			}


    </script>
    <script id="shader-terrain-fs" type="x-shader/x-fragment">
        uniform bool uUseNormalMap;
        uniform bool uUseDiffuseMap;
        precision mediump float;
        uniform sampler2D uGrassSampler;
        uniform sampler2D uRockSampler;
        uniform sampler2D uSandSampler;
        uniform sampler2D uGrassNormalSampler;
        uniform sampler2D uRockNormalSampler;
        uniform sampler2D uSandNormalSampler;

        varying vec3 normalInterp;
        varying vec3 tangentInterp;
        varying vec3 binormalInterp;

        varying vec3 position;
        varying vec3 vertPos;
        varying vec2 vTextureCoord;

		uniform float uAlpha;

        struct Light{
            vec3 Position; // Light position in eye coords.
            vec3 La; // Ambient light intensity
            vec3 Ld; // Diffuse light intensity
            vec3 Ls; // Specular light intensity
        };
        uniform Light uLight;

        struct Material{
            vec3 Ka; // Ambient light
            vec3 Kd; // Diffuse light
            vec3 Ks; // Specular light
            float Shininess;
        };

        uniform Material uMaterial;
        vec3 phongModel (vec3 vertPos, vec3 normal) {
            vec3 lightDir = normalize(uLight.Position - vertPos);
            vec3 lightReflectDir = reflect(-lightDir, normal);
            vec3 viewDir = normalize(-vertPos);
            vec3 ambient = uLight.La * uMaterial.Ka;
            float lambertian = max(dot(lightDir,normal), 0.0);
            vec3 diffuse = lambertian * uLight.Ld * uMaterial.Kd;
            vec3 specular = vec3(0.0);
            if(lambertian > 0.0) {
                float specAngle = max(dot(lightReflectDir, viewDir), 0.0);
                specular = uLight.Ls * uMaterial.Ks * pow(specAngle, uMaterial.Shininess);
            }

            return ambient + diffuse + specular;
       }
       highp float rand(vec2 co) {
            highp float a = 15.0;
            highp float b = 100.0;
            highp float c = 43758.5453;
            highp float dt= dot(co.xy ,vec2(a,b));
            highp float sn= mod(dt,3.14);
            return fract(sin(sn) * c);
        }
        vec4 getDiffuseOrCommonFragColor (vec4 textureColor, vec3 normal, vec3 vLight) {
            if (uUseDiffuseMap) {
                vec3 lightDir = normalize(uLight.Position - vertPos);
                vec3 diffuse = textureColor.rgb * uLight.Ld * max(dot(lightDir,normal), 0.0) * uMaterial.Kd;
                return vec4(diffuse, 1.0);
             }
            return vec4(textureColor.rgb * vLight, textureColor.a);
        }
       vec4 getFragmentColor (float height, vec3 normal, vec3 vLight) {
            float uTextureScale;
            float vTextureScale;
            if (height < 0.0) {
                uTextureScale = (1.0 / 32.0);
                vTextureScale = 10.0;
            } else {
                uTextureScale = (1.0 / 40.0);
                vTextureScale = 20.0;
            }
            vec4 texColorGrass = texture2D(uGrassSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale));
            vec4 texColorRock = texture2D(uRockSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale));
            vec4 texColorSand = texture2D(uSandSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale));
            vec4 texColorGrassRock;
            vec4 texColorRockSand ;

           if (height >= 0.0) {
                return getDiffuseOrCommonFragColor(texColorGrass, normal, vLight);
           }
           if (height < 0.0 && height >= -1.4) {
                texColorGrassRock = mix(texColorGrass, texColorRock, 0.4);
                return getDiffuseOrCommonFragColor(texColorGrassRock, normal, vLight);
           }
           if (height < -1.4 && height >= -2.8) {
                texColorGrassRock = mix(texColorGrass, texColorRock, 0.6);
                return getDiffuseOrCommonFragColor(texColorGrassRock, normal, vLight);
           }
           if (height < -2.8 && height >= -4.2) {
                return getDiffuseOrCommonFragColor(texColorRock, normal, vLight);
           }
           if (height < - 4.2 && height >= -5.7) {
                texColorRockSand = mix(texColorRock, texColorSand, 0.4);
                return getDiffuseOrCommonFragColor(texColorRockSand, normal, vLight);
           }
           if (height < - 5.7 && height >= -7.1) {
                texColorRockSand = mix(texColorRock, texColorSand, 0.6);
                return getDiffuseOrCommonFragColor(texColorRockSand, normal, vLight);
           }
           return gl_FragColor = getDiffuseOrCommonFragColor(texColorSand, normal, vLight);
       }
       vec3 getNormalCoordinate (float height) {
            float uTextureScale = 1.0;
            float vTextureScale = 1.0;
            if (height >= 0.0) {
                uTextureScale = (1.0 / 40.0);
                vTextureScale = 20.0;
            }
            vec3 normalCoordinateGrass = texture2D(uGrassNormalSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale)).xyz;
            vec3 normalCoordinateRock = texture2D(uRockNormalSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale)).xyz;
            vec3 normalCoordinateSand = texture2D(uSandNormalSampler, vec2(vTextureCoord.s * uTextureScale, vTextureCoord.t * vTextureScale)).xyz;

           if (height >= 0.0) {
                return normalCoordinateGrass;
           }
           if (height < 0.0 && height >= -1.4) {
                return mix(normalCoordinateGrass, normalCoordinateRock, 0.4);
           }
           if (height < -1.4 && height >= -2.8) {
                return mix(normalCoordinateGrass, normalCoordinateRock, 0.6);
           }
           if (height < -2.8 && height >= -4.2) {
                return normalCoordinateRock;
           }
           if (height < - 4.2 && height >= -5.7) {
                return mix(normalCoordinateRock, normalCoordinateSand, 0.4);
           }
           if (height < - 5.7 && height >= -7.1) {
                return mix(normalCoordinateRock, normalCoordinateSand, 0.6);
           }
           return normalCoordinateSand;
       }
        void main() {
            vec3 normal = normalize(normalInterp);

            if (uUseNormalMap && position.y < 0.0){
                vec3 tangent = normalize(tangentInterp);
                vec3 binormal = normalize(binormalInterp);

                mat3 tbn = mat3(tangent, binormal, normal);
                vec3 normalCoordinate = getNormalCoordinate(position.y);

                //Transform the normal vector in the RGB channels to tangent space
                normal = normalize(tbn * normalCoordinate);
            }
            vec3 vLight = phongModel(vertPos, normal);
            gl_FragColor = getFragmentColor(position.y, normal, vLight);
        }
    </script>

    <script type="text/javascript">
        var scene;
        var riverMap;
        var keyboardController;
        var controlValues = {
            reiniciar: function () {
                refreshScene();
            },
            ph1: 5,
            ph2: 2,
            ph3: 2,
            s1: 4,
            towerAmount: 2,
            bridgePosition: 50
        };
        var graphicalUSerInterfaceControl;
        function webGLStart() {
            try {
                graphicalUSerInterfaceControl = new GraphicalUserInterfaceControl();
                graphicalUSerInterfaceControl.start();
                riverMap = new RiverMap();
                scene = new Scene();
                riverMap.tick();
                refreshScene();
            } catch (e) {
                alert(e.name + " " + e.message);
            }
        }
        function refreshScene() {
            riverMap.setOrigin();
            scene.createSkyGroundAndCamera();
            keyboardController = new KeyboardController(scene);
            scene.tick();
        }
    </script>

</head>
<body onload="webGLStart();">
<div style="text-align: right;">
    <canvas id="scene" style="border: double transparent;" width="1024" height="720">
        Your browser does not support the HTML5 canvas element.
    </canvas>
    <canvas id="riverMap" style="border: solid#696969;" width="360" height="360">
        Your browser does not support the HTML5 canvas element.
    </canvas>
</div>
<br>
<div id="header">
    <h1>Tp Sistemas gr&aacuteficos - Ali, Luis - 2do. cuatrimestre de 2016</h1>
</div>
<div id="ref">
    <a href="#/" data-section="#camaras">Cambiar C&aacutemaras</a><br>
    <a href="#/" data-section="#CamaraOrbital">C&aacutemara orbital</a><br>
    <a href="#/" data-section="#CamaraPrimeraPersona">C&aacutemara primera persona</a>
</div>
<section id="camaras">
    <h2>Alternar C&aacutemaras</h2>
    <dl>
        <dt class="thick"><b style="color:blue;">Aletrna en el &oacuterden Orbital / Persona en el puente</b></dt>
        <dd class="italic">Tecla <b>C</b></dd>
    </dl>
</section>
<section id="CamaraOrbital">
    <h2>C&aacutemara orbital</h2>
    <dl>
        <dt class="thick">Zoom</dt>
        <dd class="italic">Acercar/Alejar, Mover rueda del mouse <b>Arriba/Abajo</b>
        <dd>
        <dt class="thick">Rotar la escena</dt>
        <dd class="italic">Mantener bot&oacuten izquierdo del mouse presionado y mover el mouse</dd>
    </dl>
</section>
<section id="CamaraPrimeraPersona">
    <h2>C&aacutemara de primera persona </h2>
    <dl>
        <dt class="thick">Desplazarse</dt>
        <dd class="italic">Izquierda/Atr&aacutes/Derecha/Adelante, <b>a/s/d/w</b> respectivamente
        <dd>
        <dt class="thick">Girar vista</dt>
        <dd class="italic">Mover el mouse manteniendo presionado el bot&oacuten izquierdo. La c&aacutemara mira al
            puntero del mouse
        </dd>
    </dl>
</section>
</section>
<div id="footer">
    Las texturas son cortes&iacutea de <a href="http://maps.jpl.nasa.gov/">Jet Propulsion Laboratory</a>.
</div>
<script>
    $("a").on("click", function () {
        var id = $(this).data("section");
        $("section:visible").fadeOut(function () {
            $(id).fadeIn();
        });
    });
    $("#scene").bind("wheel mousewheel", function (e) {
        e.preventDefault()
    });
</script>

</body>

</html>
